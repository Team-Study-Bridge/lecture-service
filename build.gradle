plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.example.lectureservice'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.10"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required.set(true)
        xml.required.set(false)
        csv.required.set(false)
        html.outputLocation.set(file("$buildDir/reports/jacoco"))
    }

    classDirectories.setFrom(
            files(fileTree(
                    dir: "$buildDir/classes/java/main",
                    includes: ['com/example/lectureservice/service/LectureService.class'],
                    excludes: ['**/config/**', '**/dto/**', '**/type/**', '**/controller/**', '**/handler/**', '**/util/**']
            ))
    )
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.3
            }
        }
    }
}

ext {
    set('springCloudVersion', "2024.0.1")  // Spring Cloud 의존성 버전 변수
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // AWS SDK for S3 (v2) - 의존성 누락 방지
    implementation 'software.amazon.awssdk:s3'

    // MySQL
    runtimeOnly 'mysql:mysql-connector-java:8.0.33'

    // 유틸
    implementation 'org.apache.commons:commons-io:1.3.2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Vault
    implementation 'org.springframework.cloud:spring-cloud-starter-vault-config:3.1.2'
}

dependencyManagement {
    imports {
        // AWS SDK 안정적 동작을 위한 BOM 추가
        mavenBom "software.amazon.awssdk:bom:2.25.20"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.1"
    }
}
